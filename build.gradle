plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'checkstyle'
	id 'com.adarshr.test-logger' version '4.0.0'
}

checkstyle {
	toolVersion '10.12.4'
	configFile file("config/checkstyle/checkstyle.xml")
	ignoreFailures = true
}
checkstyleMain {
	source ='src/main/java'
}
checkstyleTest {
	source ='src/test/java'
}

group = 'com.danieltnaves.todo'
version = '0.0.1-SNAPSHOT'

bootJar {
	archiveFileName = 'todo-service.jar'
}

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.hamcrest:hamcrest-all:1.3'
}

testlogger {
	theme 'mocha'
	showSummary true
}
testing {
	suites {
		test {
			useJUnitJupiter()
		}

		integrationTest(JvmTestSuite) {

			sources {
				java {
					compileClasspath += sourceSets.main.compileClasspath
				}
			}

			dependencies {
				implementation project()
				implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'
				implementation 'org.springframework.boot:spring-boot-starter-test'
				implementation 'org.hamcrest:hamcrest-all:1.3'
				implementation 'org.awaitility:awaitility:4.2.0'
			}

			targets {
				all {
					testTask.configure {
						shouldRunAfter(test)
					}
				}
			}

		}
	}
}

tasks.named('check') {
	dependsOn(testing.suites.integrationTest)
}